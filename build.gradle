
plugins {
    id "org.jetbrains.intellij" version "0.4.10"
    id "idea"
    id "java"
    id "jacoco"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
    maven { url 'https://jitpack.io' }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def versionsMap = ['IU-2019.2':'192.5728.98','IU-2019.3':'193.5233.144','IU-2020.1':'201.6668.99', 'IU-2020.2':'202.6397.93']

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName 'com.redhat.devtools.intellij.tekton'
    plugins 'terminal', 'yaml', 'com.intellij.kubernetes:' + versionsMap[ideaVersion]
    updateSinceUntilBuild false
}

dependencies {
    compile 'io.fabric8:openshift-client:4.12.0'
    compile 'io.fabric8:tekton-client:4.12.0'
    compile 'com.redhat.devtools.intellij:intellij-common:0.0.1-SNAPSHOT'
    testCompile 'org.mockito:mockito-inline:3.4.6'
    compile 'com.github.graphstream:gs-core:dev-SNAPSHOT'
    compile 'com.github.graphstream:gs-ui-swing:dev-SNAPSHOT'
    compile 'com.github.graphstream:gs-algo:dev-SNAPSHOT'
    compile 'com.github.vlsi.mxgraph:jgraphx:4.1.0'
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}


publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version
